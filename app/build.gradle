apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.sgtsilvio.gradle.android-retrofix'

def appPropertiesFile = rootProject.file("app-settings.properties")
def appProperties = new Properties()
appProperties.load(new FileInputStream(appPropertiesFile))
def appModeProd = appProperties["env.use.mode.production"]

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        netpos_signing_config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 30
    buildToolsVersion "29.0.3"
    buildFeatures.dataBinding = true
    ndkVersion "21.3.6528147"
    defaultConfig {
        resValue "string", "app_name", "NetPOS"
        vectorDrawables.useSupportLibrary = true
        applicationId "com.woleapp.netpos"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 53
        versionName "1.1.53"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        def environmentPath
        if (appModeProd == "true")
            environmentPath = appProperties["env.path.production"]
        else
            environmentPath = appProperties["env.path.staging"]
        buildConfigField "Boolean", "APPMODE_PROD", appModeProd
        def envPropertiesFile = rootProject.file(environmentPath)
        def envProperties = new Properties()
        envProperties.load(new FileInputStream(envPropertiesFile))
        for (String key : envProperties.keySet()) {
            buildConfigField "String", key.replaceAll("\\.", "_").toUpperCase(), envProperties[key]
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.netpos_signing_config
            buildConfigField "String", "BUILD_VARIANT", "\"debug\""
            ndk {
                abiFilters "armeabi-v7a", "x86"
            }
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "NetPOS Debug"
        }
        release {
            signingConfig signingConfigs.netpos_signing_config
            buildConfigField "String", "BUILD_VARIANT", "\"release\""
            resValue "string", "app_name", "NetPOS"
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        nibssserverdebug {
            initWith debug
            buildConfigField "String", "BUILD_VARIANT", "\"nibssServerDebug\""
            resValue "string", "app_name", "Admin Debug"
            applicationIdSuffix ".debugserver"
        }
        releaseAdmin {
            initWith release
            buildConfigField "String", "BUILD_VARIANT", "\"releaseAdmin\""
            resValue "string", "app_name", "Admin NetPOS"
            applicationIdSuffix ".releaseAdmin"
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation("commons-codec:commons-codec:1.15")
    implementation("commons-lang:commons-lang:2.6")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.5'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    //rx
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation "io.reactivex.rxjava2:rxkotlin:2.4.0"

    //nibss-client
    implementation "com.netpluspay.android.nibssclient:nibss-client:1.1.7"

    //netpos sdk
    implementation "com.netpluspay.netpossdk:netpossdk:1.0.19"
    //implementation project(path: ':netpossdk')

    //Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //android lifecycle components
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"

    //room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"

    //retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation("com.squareup.okhttp3:logging-interceptor:4.9.0")

    //easyprefs
    implementation 'com.pixplicity.easyprefs:library:1.9.0'

    //JWT
    implementation 'com.auth0:java-jwt:3.10.0'
    implementation 'com.auth0.android:jwtdecode:2.0.0'

    //viewpager2
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    //MQTT
    implementation 'com.hivemq:hivemq-mqtt-client:1.2.1'
    implementation "com.hivemq:hivemq-mqtt-client-websocket:1.2.1"
    implementation 'net.sourceforge.streamsupport:android-retrostreams:1.7.2'
    implementation 'net.sourceforge.streamsupport:android-retrofuture:1.7.2'

    implementation 'pub.devrel:easypermissions:3.0.0'
    implementation 'com.google.zxing:core:3.4.0'

//    //Eclipse MQTT
//    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.1.0'
//    implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    //paging
    implementation "androidx.paging:paging-runtime:2.1.2"

    //preference
    implementation("androidx.preference:preference-ktx:1.1.1")

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'
}
